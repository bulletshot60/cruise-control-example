package AbstractAdaptiveCruiseControlRegulator
public
	with Base_Types, AbstractCruiseControlRegulator;
	
	abstract abstract_adaptive_cruise_control_regulator extends AbstractCruiseControlRegulator::abstract_cruise_control_regulator
		annex agree {**
			assume "speed is positive or 0": current_speed >= 0;
			guarantee "speed will only increase if enabled": increase_speed = false or (enabled = true and increase_speed = true);
			guarantee "speed will only decrease if enabled": decrease_speed = false or (enabled = true and decrease_speed = true);
			guarantee "decrease or increase (not both)": (decrease_speed = false and increase_speed = false) or (decrease_speed != increase_speed);
		**};
	end abstract_adaptive_cruise_control_regulator;
	
	abstract implementation abstract_adaptive_cruise_control_regulator.impl extends AbstractCruiseControlRegulator::abstract_cruise_control_regulator.impl
		subcomponents
			increase_speed_button: refined to system increase_speed_button.impl;
			decrease_speed_button: refined to system decrease_speed_button.impl;
			
	end abstract_adaptive_cruise_control_regulator.impl;
	
	----------------------------------------------------------------------------
	
	system increase_speed_button extends AbstractCruiseControlRegulator::increase_speed_button
		annex agree {**
			guarantee "increase_speed only true when pressed": pressed = increase_speed; 
		**};
	end increase_speed_button;
	
	system implementation increase_speed_button.impl extends AbstractCruiseControlRegulator::increase_speed_button.impl
		annex agree {**
			assert increase_speed = if pressed then true else false;
		**};
	end increase_speed_button.impl;
	
	----------------------------------------------------------------------------
	
	system decrease_speed_button extends AbstractCruiseControlRegulator::decrease_speed_button
		annex agree {**
			guarantee "decrease_speed only true when pressed": pressed = decrease_speed; 
		**};
	end decrease_speed_button;
	
	system implementation decrease_speed_button.impl extends AbstractCruiseControlRegulator::decrease_speed_button.impl
		annex agree {**
			assert decrease_speed = if pressed then true else false;
		**};
	end decrease_speed_button.impl;
	
end AbstractAdaptiveCruiseControlRegulator;
